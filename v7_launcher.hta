<html>
<head>
<title>v7 ArmA Launcher 3.3</title>
<!-- !!! ID is used in code !!! -->
<HTA:APPLICATION
	ID="v7_ArmA_LauncherID"
	applicationName="ArmA Launcher"
	scroll="no"
	border="thin"
	maximizeButton="yes"
	minimizeButton="yes"
	singleInstance="yes"
	icon="v7.ico"
>
</head>

<SCRIPT Language="VBScript">
	Option Explicit

	' глобальные переменные
	Dim DBFILE ' файл с описаниями модов
	Dim optionsFile ' файл с настройками
	Dim Mods, EMods, Deps, Opts, Desc, Visible, Checked ' БД модов
	Dim WshShell, fso, f ' дескриптор файла
	Dim strEXE, strMods ' строки запуска гамы
	Dim regEx ' регулярка, которой будем проверять че-нибудь
	Dim isStarting ' пока идет старт игры, = true
	Dim oldModSum ' число в строке ввода
	Dim idTimer ' id создаваемых таймеров, чтобы было что обнулить
	Dim subFolderMode ' режим работы в подпапке
	Dim modPrefix ' префикс модов для режима работы в подпапке
	Dim Options ' опции в нижней части окна
	Dim isServer ' = true если сервер
	Dim HTTP_code, HTTP_last ' объект открытия страниц в инете, для запроса/отправки кода и проверки надо ли обновляться
	Const URL_code_get = ""
	Const URL_code_set = ""
	Const URL_last_get = ""
	Const AdminComputerName = "ADMIN"
	Dim extArgs ' аргументы, переданные ланчеру, передадим в игру
	Dim is64 ' true если винда 64-битная

	Sub Window_onLoad
		' start from TotalCmd with right mouse double click causes error on "window.resizeTo" if timeout ~< 60
		idTimer = window.setTimeout("Main", 60, "VBScript")
	End Sub

	Sub Main
		window.clearTimeout(idTimer)
		Dim width, height
		Dim s, a, i ' временные переменные
		Dim args ' аргументы скрипта
		width = 800
		height = 600

		window.resizeTo width, height
		window.moveTo (screen.width - width) / 2, (screen.height - height) / 2

		Set WshShell = CreateObject("WScript.Shell")
		Set fso = CreateObject("Scripting.FileSystemObject")
		Set regEx = New RegExp
		Set HTTP_code = CreateObject("MSXML2.XMLHTTP.3.0")
		Set HTTP_last = CreateObject("MSXML2.XMLHTTP.3.0")
		isStarting = False
		oldModSum = ""
		is64 = "AMD64" = WshShell.ExpandEnvironmentStrings("%PROCESSOR_ARCHITEW6432%")

		DBFILE = "descript.ion"
		GoToScriptDirectory
		If "v7" <> fso.GetFileName(WshShell.CurrentDirectory) Then
			subFolderMode = False
			modPrefix = ""
		Else
			subFolderMode = True
			modPrefix = "v7\"
			DBFILE = modPrefix & DBFILE
			WshShell.CurrentDirectory = WshShell.CurrentDirectory & "\.."
		End If

		password_desc.style.visibility = "hidden"
		password.style.visibility = "hidden"

		If fso.FileExists("arma3.exe") Then
			isServer = False
			headless_label.InnerHTML = ""
			password_desc.InnerHTML = ""
			password.size = 1
		ElseIf fso.FileExists("arma3server.exe") Then
			isServer = True
		Else
			isServer = ""
		End If

		optionsFile = WshShell.ExpandEnvironmentStrings("%APPDATA%") & "\v7ArmALauncher.ini"

			' ключом для всех этих БД является имя мода
		Set Mods = CreateObject("Scripting.Dictionary") ' имя мода (каталог, если в корне игры, и полный/относительный путь, в ином случае)
		Set EMods = CreateObject("Scripting.Dictionary") ' тоже, но с опцией if_exist
		Set Deps = CreateObject("Scripting.Dictionary") ' список требуемых модов
		Set Opts = CreateObject("Scripting.Dictionary")' список опций
		Set Desc = CreateObject("Scripting.Dictionary") ' описание
		Set Visible = CreateObject("Scripting.Dictionary") ' видимые в главном окне. Невидимых в этой базе нет
						' Значение: 2^n, где n - порядковый номер этого видимого мода с 0
		Set Checked = CreateObject("Scripting.Dictionary") ' отмеченные галкой выбора

		If Not fso.FileExists(DBFILE) Then
			msgbox DBFILE & " not found"
		Else
			' имя(путь) (если содержит пробелы - в кавычках), требуемые моды, опции мода, описание
			regEx.Pattern = "(""[^""]+""|[^ ]+) (?:\{([^\}]*)\})? *(?:\[([^\]]*)\])? *(.*)?"
			Set f = fso.OpenTextFile(DBFILE)
			Do Until f.AtEndOfStream
				s = LTrim(f.ReadLine)
				If s <> "" And "#" <> Left(s, 1) Then
					Set a = regEx.Execute(s)
					If 0 < a.Count Then
						' добавляем без кавычек
						i = Replace(a(0).SubMatches(0), """", "")
						Deps.Add i, a(0).SubMatches(1)
						Opts.Add i, a(0).SubMatches(2)
						Desc.Add i, a(0).SubMatches(3)
						If ModHasOption(i, "if_exist") Then
							EMods.Add i, modPrefix & i
						Else
							Mods.Add i, modPrefix & i
						End If
					Else
						msgbox """" & s & """ no regex"
					End If
				End If
			Loop
			f.Close
			' добавляем EMods в конец Mods
			For Each i In EMods.Keys
				Mods.Add i, EMods(i)
			Next
		End If

		If subFolderMode And fso.FileExists(modPrefix & "found_addons.txt") Then
			Set f = fso.OpenTextFile(modPrefix & "found_addons.txt")
			Do Until f.AtEndOfStream
				s = f.ReadLine
				a = f.ReadLine
				For Each i In Array("", "!Workshop\")
					If fso.FolderExists(i & a) Then
						If Mods.Exists(s) Then
							Mods(s) = i & a
						Else
							Mods.Add s, i & a
							Opts.Add s, "dont_show"
						End If
						Exit For
					End If
				Next
			Loop
			f.Close
		End If

		' удаляем несуществующие EMods из Mods
		For Each i In EMods.Keys
			If Not ModExists(i) Then Mods.Remove i
		Next

		Dim Colors(2), currentColor ' цвета чередующихся строк таблицы
		' infobackground silver lightskyblue bisque  cornsilk lightcyan
		Colors(0) = "lightcyan"
		Colors(1) = "cornsilk"
		currentColor = 0
		Dim HTMLTable ' исх. текст таблицы модов
		HTMLTable = "<table align='center' border=0 cellspacing=5 cellpadding=0 style='padding-left:5;padding-right:5'>"
		a = 1	' значение "Num" каждого видимого мода
		For Each i In Mods.Keys
			If Not ModHasOption(i, "with:") And Not ModHasOption(i, "dont_show") Then
				Visible.Add i, a
				a = a * 2
				If EMods.Exists(i) Then
					s = "* " ' пометим звездой моды с опцией if_exist
				Else
					s = ""
				End If
				' в таблице чекбоксы имеют id = имени мода, например:
				'<label for='_new'><tr bgcolor='cyan'><td><input id='_new' type='checkbox' onclick='ModOnClick'>new</td><td>Descr</td></tr></label>
				HTMLTable = HTMLTable & _
					"<label for='" & i & "'><tr bgcolor='"& Colors(currentColor) & "'>" & _
					"<td><input id='" & i & "' type='checkbox' onclick='ModOnClick'>" & s & i & "</td>" & _
					"<td>" & Desc(i) & "</td>" & _
					"<td>" & Visible(i) & "</td>" & _
					"</tr></label>"
				currentColor = 1 - currentColor
			End If
		Next
		HTMLTable = HTMLTable & "</table>"
		ModsTable.InnerHTML = HTMLTable
		' пройдемся по модам в таблице в поисках дефолтно-отмечаемых
		For Each i In Visible.Keys
			If Not ModHasOption(i, "not_default") And Not ModHasOption(i, "if_exist") Then
				ModCheck(i)
			End If
		Next

		If AdminComputerName = WshShell.ExpandEnvironmentStrings("%ComputerName%") Then
			filePatching_label.style.visibility = "visible"
		End If

		Set Options = CreateObject("Scripting.Dictionary")
		' значение: регулярка, дефолтное значение, является ли аргументом для arma
		Options.Add "startBefore", Array(".*", "", false)
		'Options.Add "cpuCount", Array("[0-8]", "0", true)
		'Options.Add "exThreads", Array("0|1|3|5|7", "0", true)
		'Options.Add "maxMem", Array("0|768|1024|2047|4096|8192|16384|32768", "0", true)
		Options.Add "Priority", Array("0|-?[12]", "0", false)
		Options.Add "showScriptErrors", Array("[01]", "0", true)
		Options.Add "nologs", Array("[01]", "1", true)
		If is64 Then
			s = "1"
		Else
			s = "0"
		End If
		Options.Add "x64", Array("[01]", s, false)
		Options.Add "filePatching", Array("[01]", "0", true)

		For Each s In Options.Keys
			Call SetHTMLValue(s, Options(s)(1))
		Next
		LoadOptions

		If "String" <> TypeName(isServer) And "" <> URL_code_get Then
			If Not isServer Then
				HTTP_code.open "GET", URL_code_get & fso.GetTempName
				HTTP_code.onreadystatechange = GetRef("onreadyHTTP_code")
				code_status.InnerHTML = "--"
				code_status.title = "Code checking..."
				HTTP_code.send
			End If
		End If

		extArgs = Mid(v7_ArmA_LauncherID.commandLine, 1 + InStr(2, v7_ArmA_LauncherID.commandLine, """", vbBinaryCompare))
		regEx.Pattern = "^\s+start"
		If regEx.Test(extArgs) Then
			extArgs = regEx.Replace(extArgs, "")
			Launch
		End If

		If "" <> URL_last_get Then
			HTTP_last.open "GET", URL_last_get & fso.GetTempName
			HTTP_last.onreadystatechange = GetRef("onreadyHTTP_last")
			idUpdate.value = "Checking..."
			HTTP_last.send
		End If
	End Sub

	Sub onreadyHTTP_last
		' вызывается, когда готов результат http-запроса штампа
		If 4 <> HTTP_last.readyState Then Exit Sub
		If 200 <> HTTP_last.status Then
			idUpdate.value = "ERROR :("
			idUpdate.title = "HTTP_last.status=" & HTTP_last.status
			Exit Sub
		End If
		If fso.FileExists(modPrefix & "last") Then
			If HTTP_last.responseText = fso.OpenTextFile(modPrefix & "last").ReadAll Then
				idUpdate.value = "No updates :)"
				idUpdate.title = "HTTP_last.responseText=" & Left(HTTP_last.responseText, 30)
				Exit Sub
			End If
		End If
		UpdateBlink ' из-за косяка (см. ниже) здесь прямой вызов вместо setTimeout
	End Sub

	Sub UpdateBlink
		' моргает кнопкой Update
		' здесь какой-то косяк, в отличие от документации событие по setTimeout не повторяется бесконечно
		Const str = "--> Update <--"
		Dim time
		If str <> idUpdate.value Then
			idUpdate.value = str
			time = 600
		Else
			idUpdate.value = "                           "
			time = 300
		End If
		window.setTimeout "UpdateBlink", time, "VBScript"
	End Sub

	Function ModExists(m)
		' проверяет существование мода с опцией if_exist или его зависимости (рекурсивно)
		Dim ret, dep
		If Not Mods.Exists(m) Then
			ret = fso.FolderExists(modPrefix & m)
		Else
			If Not ModHasOption(m, "only_required") Then
				ret = fso.FolderExists(Mods(m))
			Else
				ret = True
				For Each dep In Split(Deps(m), ";")
					If Not ModExists(dep) Then
						ret = False
						Exit For
					End If
				Next
			End If
		End If
		ModExists = ret
	End Function

	Function ModHasOption(m, opt)
		' возвращает, есть ли у данного мода данная опция
		Dim ret
		'  dont_show  only_required  not_default  with:aaa,ddd,fff  if_exist
		If InStr(1, Opts(m), opt, vbTextCompare) Then
			ret = True
		Else
			ret = False
		End If
		ModHasOption = ret
	End Function

	Function IsModInList(list, strMod)
		' возвращает, находится ли в данном списке модов данный мод
		Dim ret, s
		ret = False
		For Each s In Split(list, ";")
			If s = strMod Then
				ret = True
				Exit For
			End If
		Next
		IsModInList = ret
	End Function

	Sub ModOnClick
		' запускается при нажатии на галку любого мода
		If window.event.srcelement.checked Then
			ModCheck(window.event.srcelement.id)
		Else
			ModUnCheck(window.event.srcelement.id)
		End If
	End Sub

	Sub ModCheck(m)
		' отмечает текущий мод, как выбранный, а также все ему требуемые
		Dim i
		If Not Checked.Exists(m) Then Checked.Add m, Null
		document.getElementById(m).checked = True
		' менять значение суммы будем только, если текущий мод в нее не включен
		'используем побитовое И
		If (CLng(Visible(m)) And CLng(ModSum.value)) = 0 Then ModSum.value = CLng(ModSum.value) + CLng(Visible(m))
		' включим все требуемые моды
		For Each i In Visible.Keys
			If IsModInList(Deps(m), i) And Not Checked.Exists(i) Then ModCheck(i)
		Next
	End Sub

	Sub ModUnCheck(m)
		' снимает с текущего мода отметку выбранности. И заодно со всех модов, которые требуют его
		Dim i
		If Checked.Exists(m) Then Checked.Remove m
		document.getElementById(m).checked = False
		' менять значение суммы будем только, если текущий мод в нее включен
		'используем побитовое И
		If (CLng(Visible(m)) And CLng(ModSum.value)) > 0 Then ModSum.value = CLng(ModSum.value) - CLng(Visible(m))
		' выключим все зависимые моды
		For Each i In Visible.Keys
			If IsModInList(Deps(i), m) And Checked.Exists(i) Then ModUnCheck(i)
		Next
	End Sub

	Sub ModSum_KeyDown
		' запускается при нажатии любой клавиши в поле ввода суммы-кода модов
		If 13 = window.event.Keycode Then Exit Sub
		If "" = oldModSum Then
			' запомним старое значение
			oldModSum = ModSum.value
			idStartButton.Disabled = True
		Else
			window.clearTimeout(idTimer)
		End If
		idTimer = window.setTimeout("ModSum_Change", 1000, "VBScript")
	End Sub

	Sub ModSum_Change
		' запускается из функции ModSum_KeyDown с задержкой, чтобы KeyDown изменило поле ModSum
		Dim i
		window.clearTimeout(idTimer)
		ModSum.ReadOnly = True
		regEx.Pattern = "^[\d]+$"
		' если новое значение не цифры, просто вернем старое
		If Not regEx.Test(ModSum.value) Then
			ModSum.value = oldModSum
		ElseIf CLng(oldModSum) <> CLng(ModSum.value) Then
			'проходимся по модам в таблице, если надо отметить - отм., надо снять - снять
			For Each i In Visible.Keys
				'используем побитовое И
				If (CLng(Visible(i)) And CLng(ModSum.value)) > 0 Then
					If Not Checked.Exists(i) Then ModCheck(i)
				Else
					If Checked.Exists(i) Then ModUnCheck(i)
				End If
			Next
		End If
		oldModSum = ""
		ModSum.ReadOnly = False
		idStartButton.Disabled = False
	End Sub

	Sub AddModToString(path)
		' проверяет, существует ли каталог мода, и добавляет его к строке модов
		If IsModInList(strMods, path) Then Exit Sub
		If Not fso.FolderExists(path) Then msgbox path & " added, but folder not found"
		strMods = strMods & ";" & path
	End Sub

	Sub AddModAndDepsToString(m)
		' добавляет в строку strMods данный мод и, если есть, его зависимости (рекурсивно)
		Dim dep
		If Not Mods.Exists(m) Then
			' если мода в базе нет, просто добавим его
			AddModToString(modPrefix & m)
			Exit Sub
		End If
		' если мод в базе, вызовем рекурсию для всех зависимостей и добавим его самого
		For Each dep In Split(Deps(m), ";")
			AddModAndDepsToString(dep)
		Next
		If Not ModHasOption(m, "only_required") Then AddModToString(Mods(m))
	End Sub

	Sub AddModsWith_with
		' добавляет, если надо, в строку модов моды с опцией with
		Dim m, modAdded, withList, withMod
		modAdded = True ' переменная показывает, произошло ли добавление текущего мода к списку
		Do While modAdded ' и пока моды будут добавляться, мы будем проходить по всем модам с with заново
			modAdded = False
			For Each m In Mods.Keys
				If ModHasOption(m, "with:") And Not IsModInList(strMods, Mods(m)) Then
					' уберем впереди стоящую "with:"
					withList = Split(Opts(m), ":")(1)
					modAdded = True
					For Each withMod In Split(withList, ",")
						' если мод в базе, проверяем реальное имя, иначе текущее
						If Mods.Exists(withMod) Then
							If Not IsModInList(strMods, Mods(withMod)) Then
								modAdded = False
								Exit For
							End If
						Else
							If Not IsModInList(strMods, modPrefix & withMod) Then
								modAdded = False
								Exit For
							End If
						End If
					Next
					If modAdded Then AddModAndDepsToString(m)
				End If
			Next
		Loop
	End Sub

	Sub myQuit
		window.close() : WScript.Quit
	End Sub

	Sub Launch
		' запускается при нажатии на кнопку запуска игры
		If isStarting Then
			msgbox "Already starting"
			Exit Sub
		End If
		isStarting = True
		idStartButton.Disabled = True
		ModSum.ReadOnly = True

		Dim s, i, StartPriorities
		' параметры для команды start
		StartPriorities = Array("low", "belownormal", "normal", "abovenormal", "high")

		If "String" = TypeName(isServer) Then
			msgbox "exe not found"
			myQuit
		End If

		' check if arma path is correct
		If Not isServer Then
			s = ""
			On Error Resume Next
			s = WshShell.RegRead("HKLM\SOFTWARE\bohemia interactive\arma 3\main")
			On Error Goto 0
			If s = "" Then
				msgbox "reg key not found"
			Else
				If LCase(s) <> LCase(WshShell.CurrentDirectory) Then
					msgbox "Wrong path!" & _
						vbNewLine & "Registry path: " & s & _
						vbNewLine & "Current path: " & WshShell.CurrentDirectory & _
						vbNewLine & vbNewLine & vbNewLine & "Press Ctrl+C to copy this message"
				End If
			End If
		End If

		If 0 = GetHTMLValue("x64") Then
			strEXE = ""
		Else
			strEXE = "_x64"
		End If
		If Not isServer Then
			strEXE = "arma3" & strEXE & ".exe"
		Else
			If headless.checked Then
				strEXE = "arma3server" & strEXE & ".exe ""-profiles=hcprofiles"" -client -connect=127.0.0.1 -password=" & password.value
			Else
				strEXE = "arma3server" & strEXE & ".exe ""-profiles=profiles"" -config=server.cfg -loadMissionToMemory"
				If fso.FolderExists("servermod\addons") Then
					strEXE = strEXE & " -servermod=servermod"
				End If
				If "" <> URL_code_set Then
					HTTP_code.open "GET", URL_code_set & ModSum.value & "&rand=" & fso.GetTempName
					HTTP_code.onreadystatechange = GetRef("onreadyHTTP_code")
					code_status.InnerHTML = "--"
					code_status.title = "Code " & ModSum.value & " uploading..."
					HTTP_code.send
				End If
			End If
		End If
		strEXE = strEXE & " -nosplash -world=empty -skipIntro -noPause"
		strEXE = "cmd /c start """" /" & StartPriorities(2+CInt(GetHTMLValue("Priority"))) & " " & strEXE
		strMods = ""
		' сначала пройдемся по всем отмеченным модам
		For Each i In Mods.Keys
			If Checked.Exists(i) Then AddModAndDepsToString(i)
			If (i = "@pooter_enhanced_asr_ai" Or i = "@Vcom_AI") And Checked.Exists(i) Then strEXE = strEXE & " -filePatching"
		Next
		' потом по модам с опцией with:
		AddModsWith_with
		If fso.FolderExists("R:\@_RAM") Then strMods = strMods & ";" & "R:\@_RAM"
		strEXE = strEXE & GetOptionsString
		' моды в кавычки на случай пробелов
		If ";" = Left(strMods, 1) Then strMods = " ""-mod=" & Mid(strMods, 2) & """"

		s = GetHTMLValue("startBefore")
		If s <> "" Then
			If LCase("msg ") = LCase(Left(s,4)) Then
				MsgBox Mid(s,5)
			Else
				WshShell.Run s, , True
			End If
		End IF
		'msgbox strEXE & strMods & extArgs
		WshShell.Run strEXE & strMods & extArgs, , True
		idTimer = window.setTimeout("Launch_finished", 10000, "VBScript")
		'Launch_finished
		'myQuit
	End Sub

	Sub Launch_finished
		' запускается из функции Launch
		window.clearTimeout(idTimer)
		ModSum.ReadOnly = False
		idStartButton.Disabled = False
		isStarting = False
	End Sub

	Sub onreadyHTTP_code
		' вызывается, когда готов результат http-запроса штампа
		If 4 <> HTTP_code.readyState Then Exit Sub
		If 200 <> HTTP_code.status Then
			code_status.InnerHTML = "ER"
			code_status.title = "HTTP_code.status=" & HTTP_code.status
			Exit Sub
		End If
		If isServer Then
			If HTTP_code.responseText <> ModSum.value Then
				code_status.InnerHTML = "ER"
				code_status.title = "HTTP_code.responseText=" & Left(HTTP_code.responseText, 30)
				Exit Sub
			End If
			code_status.title = "Code " & ModSum.value & " uploaded"
		Else
			If "" = oldModSum Then
				oldModSum = ModSum.value
				idStartButton.Disabled = True
			End If
			ModSum.value = Left(HTTP_code.responseText, 9)
			code_status.title = "server code = " & ModSum.value
			ModSum_Change
		End If
		code_status.InnerHTML = "OK"
	End Sub

	Sub OnKeyDown
		' запускается при нажатии любой клавиши в окне
		If window.event.Keycode = 27 Then myQuit
		If window.event.Keycode = 13 And Not idStartButton.Disabled And "idStartButton" <> window.event.srcElement.id Then Launch
	End Sub

	Function GetHTMLValue(id)
		' возвращает значение данного элемента (в т.ч. checkbox)
		Dim ret
		If "checkbox" <> document.getElementById(id).type Then
			ret = document.getElementById(id).value
		Else
			If document.getElementById(id).checked Then
				ret = "1"
			Else
				ret = "0"
			End If
		End If
		GetHTMLValue = ret
	End Function

	Sub SetHTMLValue(id, val)
		' устанавливает для данного элемента данное значение (в т.ч. checkbox)
		If "checkbox" <> document.getElementById(id).type Then
			document.getElementById(id).value = val
		Else
			If "1" = val Then
				document.getElementById(id).checked = True
			Else
				document.getElementById(id).checked = False
			End If
		End If
	End Sub

	Function GetOptionsString
		' возвращает строку с параметрами, соответствующими текущим опциям запуска
		Dim ret, opt
		ret = ""
		For Each opt In Options.Keys
			If Options(opt)(2) Then
				If "checkbox" = document.getElementById(opt).type Then
					If "1" = GetHTMLValue(opt) Then ret = ret & " -" & opt
				Else
					If 0 <> GetHTMLValue(opt) Then ret = ret & " -" & opt & "=" & GetHTMLValue(opt)
				End If
			End If
		Next
		GetOptionsString = ret
	End Function

	Sub SaveOnClick
		' при нажатии кнопки "Сохранить"
		Dim opt
		Set f = fso.CreateTextFile(optionsFile, True)
		f.WriteLine("[User]")
		For Each opt In Options.Keys
			f.WriteLine(opt & "=" & GetHTMLValue(opt))
		Next
		f.Close
	End Sub

	Sub LoadOptions
		' загружает опции из файла
		Dim s, a
		If Not fso.FileExists(optionsFile) Then Exit Sub
		Set f = fso.OpenTextFile(optionsFile)
		Do Until f.AtEndOfStream
			s = f.ReadLine
			a = Split(s, "=")
			If 1 = UBound(a) Then
				If Options.Exists(a(0)) Then
					regEx.Pattern = Options(a(0))(0)
					If regEx.Test(a(1)) Then
						Call SetHTMLValue(a(0), a(1))
					Else
						Call SetHTMLValue(a(0), Options(a(0))(1))
					End If
				End If
			End If
		Loop
		f.Close
	End Sub

	Sub LoadOnClick
		' при нажатии кнопки "Загрузить"
		If Not fso.FileExists(optionsFile) Then
			msgbox "save not found"
			Exit Sub
		End If
		LoadOptions
	End Sub

	Sub HeadlessOnClick
		' при изменении галки Headless
		If headless.checked Then
			password_desc.style.visibility = "visible"
			password.style.visibility = "visible"
			password.focus
		Else
			password_desc.style.visibility = "hidden"
			password.style.visibility = "hidden"
		End If
	End Sub

	Sub UpdateOnClick
		' при нажатии кнопки "Update"
		GoToScriptDirectory
		WshShell.CurrentDirectory = WshShell.CurrentDirectory & "\cwRsync\"
		WshShell.Run """Download_from_inet.cmd"" launcher", , True
	End Sub

	Sub GoToScriptDirectory
		' меняет текущий каталог на каталог, где лежит скрипт
		Dim path
		path = document.location.href
		path = fso.GetParentFolderName(UnEscape(Replace(Right(path, Len(path) - 8), "/", "\")))
		WshShell.CurrentDirectory = path
	End Sub
</SCRIPT>

<body bgcolor="buttonface" onKeyDown="OnKeyDown">
	<span id="ModsTable" style="overflow: auto; width: 770px; height: 400;"></span>
	<br><br>
	<div align="center">
		<span id="code_status"></span>
		<input type="text" value="0" maxlength="8" size="15" id="ModSum" onKeyDown="ModSum_KeyDown">
		<input type="button" value="Start game" title="Start game" id="idStartButton" onClick="Launch">
		<label for="headless" id="headless_label"><input id="headless" type="checkbox" onClick="HeadlessOnClick">Headless Client</label>
		<span id="password_desc" title="Server password">pass:</span><input type="text" value="777" maxlength="200" size="10" id="password" title="Server password">
		<input type="button" value="Update" title="Update" id="idUpdate" onClick="UpdateOnClick">
	</div>
	<fieldset>
		<legend>Personal settings</legend>
		<div align="center">
			Start before
			<input type="text" value="" maxlength="2000" size="100" id="startBefore">
			<input type="button" value="  X  " onClick="startBefore.value=''">
			<br />
			<label for="x64"><input id="x64" type="checkbox">x64</label>
			<!--cpuCount
			<select id="cpuCount">
				<option value=0>not set
				<option value=1>1
				<option value=2>2
				<option value=3>3
				<option value=4>4
				<option value=5>5
				<option value=6>6
				<option value=7>7
				<option value=8>8
				<option value=9>9
				<option value=10>10
				<option value=11>11
				<option value=12>12
				<option value=13>13
				<option value=14>14
				<option value=15>15
				<option value=16>16
			</select>
			--><!--maxMem
			<select id="maxMem">
				<option value=0>not set
				<option value=768>768
				<option value=1024>1024
				<option value=2047>2047
				<option value=4096>4096
				<option value=8192>8192
				<option value=16384>16384
				<option value=32768>32768
			</select>
			-->Priority
			<select id="Priority">
				<option value="-2">low
				<option value="-1">below normal
				<option value="0">normal
				<option value="1" selected="">above normal
				<option value="2">high
			</select>
			<label for="showScriptErrors"><input id="showScriptErrors" type="checkbox">Show script errors</label>
			<label for="nologs"><input id="nologs" type="checkbox">No logs</label>
			<label for="filePatching" id="filePatching_label" style="visibility: hidden"><input id="filePatching" type="checkbox">FP</label>
			<br /><br />
			<input type="button" value="Save" onClick="SaveOnClick">
			<input type="button" value="Load" onClick="LoadOnClick">
		</div>
	</fieldset>
</body>
</html>
