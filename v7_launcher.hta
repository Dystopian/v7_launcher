<html>
<head>
<title>v7 ArmA Launcher 3.3</title>
<HTA:APPLICATION
	ID="v7_ArmA_LauncherID"
	applicationName="ArmA Launcher"
	scroll="no"
	border="thin"
	maximizeButton="yes"
	minimizeButton="yes"
	singleInstance="yes"
	icon="arma3launcher.exe"
>
</head>

<SCRIPT Language="VBScript">
	Option Explicit

	' глобальные переменные
	Dim DBFILE ' файл с описаниями модов
	Dim optionsFile ' файл с настройками
	Dim M,Deps,Opts,Desc,Visible,Checked ' БД модов
	Dim WshShell,fso,f ' дескриптор файла
	Dim strEXE, strMods ' строки запуска гамы
	Dim regEx ' регулярка, которой будем проверять че-нибудь
	Dim isStarting ' пока идет старт игры, = true
	Dim oldModSum ' число в строке ввода
	Dim idTimer ' id создаваемых таймеров, чтобы было что обнулить
	Dim subFolderMode ' режим работы в подпапке
	Dim modPrefix ' префикс модов для режима работы в подпапке
	Dim Options ' опции в нижней части окна
	Dim cacheFilePath ' путь к файлу с AnimDataCache и имя файла
	Const cacheFileName = "6ff2c91729fc5490b726322f1f9577f672b80002.dat"
	Dim cacheTimer ' id таймера для сохранения AnimDataCache

	Sub Window_onLoad
		' start from TotalCmd with right mouse double click causes error on "window.resizeTo" if timeout ~< 60
		idTimer = window.setTimeout( "Main", 60, "VBScript")
	End Sub

	Sub Main
		window.clearTimeout(idTimer)
		Dim width, height
		Dim s, a, i ' временные переменные
		Dim args ' аргументы скрипта
		width = 800
		height = 600

		window.resizeTo width,height
		window.moveTo (screen.width - width) / 2, (screen.height - height) / 2

		Set WshShell = CreateObject("WScript.Shell")
		Set fso = CreateObject("Scripting.FileSystemObject")
		Set regEx = New RegExp
		isStarting = False
		oldModSum = ""

		DBFILE = "descript.ion"
		If "v7" <> fso.GetFileName(WshShell.CurrentDirectory) Then
			subFolderMode = False
			modPrefix = ""
		Else
			subFolderMode = True
			modPrefix = "v7\"
			DBFILE = modPrefix & DBFILE
			WshShell.CurrentDirectory = WshShell.CurrentDirectory & "\.."
		End If

		password_desc.style.visibility = "hidden"
		password.style.visibility = "hidden"
		If fso.FileExists("arma3.exe") Then
			headless_label.InnerHTML=""
			password_desc.InnerHTML=""
			password.size=1
		End If

		optionsFile = WshShell.ExpandEnvironmentStrings("%APPDATA%") & "\v7ArmALauncher.ini"

			' ключом для всех этих БД является имя мода
		Set M = CreateObject("Scripting.Dictionary") ' имя мода (каталог, если в корне игры, и полный/относительный путь, в ином случае)
		Set Deps = CreateObject("Scripting.Dictionary") ' список требуемых модов
		Set Opts = CreateObject("Scripting.Dictionary")' список опций
		Set Desc = CreateObject("Scripting.Dictionary") ' описание
		Set Visible = CreateObject("Scripting.Dictionary") ' видимые в главном окне. Невидимых в этой базе нет
						' Значение: 2^n, где n - порядковый номер этого видимого мода с 0
		Set Checked = CreateObject("Scripting.Dictionary") ' отмеченные галкой выбора

		If Not fso.FileExists(DBFILE) Then
			msgbox DBFILE & " not found"
		Else
			' имя(путь) (если содержит пробелы - в кавычках), требуемые моды, опции мода, описание
			regEx.Pattern = "(""[^""]+""|[^ ]+) (?:\{([^\}]*)\})? *(?:\[([^\]]*)\])? *(.*)?"
			Set f = fso.OpenTextFile(DBFILE)
			Do Until f.AtEndOfStream
				s = f.ReadLine
				If s <> "" And "#"<>Left(s,1) Then
					Set a = regEx.Execute(s)
					If 0 < a.Count Then
						' добавляем без кавычек
						i = Replace(a(0).SubMatches(0),"""","")
						M.Add i, modPrefix & i
						Deps.Add i, a(0).SubMatches(1)
						Opts.Add i, a(0).SubMatches(2)
						Desc.Add i, a(0).SubMatches(3)
					Else
						msgbox """" & s & """ no regex"
					End If
				End If
			Loop
			f.Close
		End If

		If subFolderMode And fso.FileExists(modPrefix & "found_addons.txt") Then
			Set f = fso.OpenTextFile(modPrefix & "found_addons.txt")
			Do Until f.AtEndOfStream
				s = f.ReadLine
				a = f.ReadLine
				For Each i In Array("","!Workshop\")
					If fso.FolderExists(i & a) Then
						If M.Exists(s) Then
							M(s) = i & a
						Else
							M.Add s, i & a
							Opts.Add s,"dont_show"
						End If
						'msgbox s&"="&M(s)
						Exit For
					End If
				Next
			Loop
			f.Close
		End If

		Dim Colors(2), currentColor ' цвета чередующихся строк таблицы
		' infobackground silver lightskyblue bisque  cornsilk lightcyan
		Colors(0) = "lightcyan"
		Colors(1) = "cornsilk"
		currentColor = 0
		Dim HTMLTable ' исх. текст таблицы модов
		HTMLTable = "<table align='center' border=0 cellspacing=5 cellpadding=0 style='padding-left:5;padding-right:5'>"
		a = 1	' значение "Num" каждого видимого мода
		For Each i In M.Keys
			If Not ModHasOption(i,"with:") And Not ModHasOption(i,"dont_show") Then
				Visible.Add i, a
				a = a * 2
				' в таблице чекбоксы имеют id = имени мода, например:
				'<label for='_new'><tr bgcolor='cyan'><td><input id='_new' type='checkbox' onclick='ModOnClick'>new</td><td>Descr</td></tr></label>
				HTMLTable = HTMLTable & _
					"<label for='" & i & "'><tr bgcolor='"& Colors(currentColor) & "'>" & _
					"<td><input id='" & i & "' type='checkbox' onclick='ModOnClick'>" & i & "</td>" & _
					"<td>" & Desc(i) & "</td>" & _
					"<td>" & Visible(i) & "</td>" & _
					"</tr></label>"
				currentColor = 1 - currentColor
			End If
		Next
		HTMLTable = HTMLTable & "</table>"
		ModsTable.InnerHTML = HTMLTable
		' пройдемся по модам в таблице в поисках дефолтно-отмечаемых
		For Each i In Visible.Keys
			If Not ModHasOption(i,"not_default") Then
				ModCheck(i)
			End If
		Next

		Set Options = CreateObject("Scripting.Dictionary")
		' значение: регулярка, дефолтное значение, является ли аргументом для arma
		Options.Add "startBefore", Array(".*","",false)
		Options.Add "cpuCount", Array("[0-8]","0",true)
		'Options.Add "exThreads", Array("0|1|3|5|7","0",true)
		'Options.Add "maxMem", Array("0|768|1024|2047|4096|8192|16384|32768","0",true)
		Options.Add "Priority", Array("0|-?[12]","0",false)
		Options.Add "showScriptErrors", Array("[01]","0",true)
		Options.Add "nologs", Array("[01]","1",true)
		If "AMD64" = WshShell.ExpandEnvironmentStrings("%PROCESSOR_ARCHITEW6432%") Then
			s="1"
		Else
			s="0"
		End If
		Options.Add "x64", Array("[01]",s,false)

		For Each s In Options.Keys
			Call SetHTMLValue(s,Options(s)(1))
		Next 
		LoadOptions

		cacheFilePath = WshShell.ExpandEnvironmentStrings("%LOCALAPPDATA%") & "\Arma 3\AnimDataCache\"

		args = Split(v7_ArmA_LauncherID.commandLine)
		If "start" = args(Ubound(args)) Then
			Launch
		End If
	End Sub

	Function ModHasOption(mo,opt)
		' возвращает, есть ли у данного мода данная опция
		Dim ret
		'  dont_show  only_required  not_default  with:aaa,ddd,fff
		If InStr(1,Opts(mo),opt,vbTextCompare) Then
			ret = True
		Else
			ret = False
		End If
		ModHasOption = ret
	End Function

	Function IsModInList(list,strMod)
		' возвращает, находится ли в данном списке модов данный мод
		Dim ret, s
		ret = False
		For Each s In Split(list,";")
			If s = strMod Then
				ret = True
				Exit For
			End If
		Next
		IsModInList = ret
	End Function

	Sub ModOnClick
		' запускается при нажатии на галку любого мода
		If window.event.srcelement.checked Then
			ModCheck(window.event.srcelement.id)
		Else
			ModUnCheck(window.event.srcelement.id)
		End If
	End Sub

	Sub ModCheck(mo)
		' отмечает текущий мод, как выбранный, а также все ему требуемые
		Dim i
		If Not Checked.Exists(mo) Then Checked.Add mo, Null
		document.getElementById(mo).checked = True
		' менять значение суммы будем только, если текущий мод в нее не включен
		'используем побитовое И
		If (CLng(Visible(mo)) And CLng(ModSum.value)) = 0 Then ModSum.value = CLng(ModSum.value) + CLng(Visible(mo))
		' включим все требуемые моды
		For Each i In Visible.Keys
			If IsModInList(Deps(mo),i) And Not Checked.Exists(i) Then ModCheck(i)
		Next
	End Sub

	Sub ModUnCheck(mo)
		' снимает с текущего мода отметку выбранности. И заодно со всех модов, которые требуют его
		Dim i
		If Checked.Exists(mo) Then Checked.Remove mo
		document.getElementById(mo).checked = False
		' менять значение суммы будем только, если текущий мод в нее включен
		'используем побитовое И
		If (CLng(Visible(mo)) And CLng(ModSum.value)) > 0 Then ModSum.value = CLng(ModSum.value) - CLng(Visible(mo))
		' выключим все зависимые моды
		For Each i In Visible.Keys
			If IsModInList(Deps(i),mo) And Checked.Exists(i) Then ModUnCheck(i)
		Next
	End Sub

	Sub ModSum_KeyDown
		' запускается при нажатии любой клавиши в поле ввода суммы-кода модов
		If window.event.Keycode <> 13 Then
			If "" = oldModSum Then
				' запомним старое значение
				oldModSum = ModSum.value
				idStartButton.Disabled = True
			Else
				window.clearTimeout(idTimer)
			End If
			idTimer = window.setTimeout( "ModSum_Change", 1000, "VBScript")
		End If
	End Sub

	Sub ModSum_Change
		' запускается из функции ModSum_KeyDown с задержкой, чтобы KeyDown изменило поле ModSum
		Dim i
		window.clearTimeout(idTimer)
		ModSum.ReadOnly = True
		regEx.Pattern = "^[\d]+$"
		' если новое значение не цифры, просто вернем старое
		If Not regEx.Test(ModSum.value) Then
			ModSum.value = oldModSum
		ElseIf CLng(oldModSum)<>CLng(ModSum.value) Then
			'проходимся по модам в таблице, если надо отметить - отм., надо снять - снять
			For Each i In Visible.Keys
				'используем побитовое И
				If (CLng(Visible(i)) And CLng(ModSum.value)) > 0 Then
					If Not Checked.Exists(i) Then ModCheck(i)
				Else
					If Checked.Exists(i) Then ModUnCheck(i)
				End If
			Next
		End If
		oldModSum = ""
		ModSum.ReadOnly = False
		idStartButton.Disabled = False
	End Sub

	Sub AddModToString(path)
		' проверяет, существует ли каталог мода, и добавляет его к строке модов
		If Not IsModInList(strMods,path) Then
			If Not fso.FolderExists(path) Then msgbox path & " added, but folder not found"
			strMods = strMods & ";" & path
		End If
	End Sub

	Sub AddModAndDepsToString(mo)
		' добавляет в строку strMods данный мод и, если есть, его зависимости (рекурсивно)
		Dim dep
		If Not M.Exists(mo) Then
			' если мода в базе нет, просто добавим его
			AddModToString(modPrefix & mo)
		Else
			' если мод в базе, вызовем рекурсию для всех зависимостей и добавим его самого
			For Each dep In Split(Deps(mo),";")
				AddModAndDepsToString(dep)
			Next
			If Not ModHasOption(mo,"only_required") Then AddModToString(M(mo))
		End If
	End Sub

	Sub AddModsWith_with
		' добавляет, если надо, в строку модов моды с опцией with
		Dim mo, modAdded, withList, withMod
		modAdded = True ' переменная показывает, произошло ли добавление текущего мода к списку
		Do While modAdded ' и пока моды будут добавляться, мы будем проходить по всем модам с with заново
			modAdded = False
			For Each mo In M.Keys
				If ModHasOption(mo,"with:") And Not IsModInList(strMods,M(mo)) Then
					' уберем впереди стоящую "with:"
					withList = Split(Opts(mo),":")(1)
					modAdded = True
					For Each withMod In Split(withList,",")
						' если мод в базе, проверяем реальное имя, иначе текущее
						If M.Exists(withMod) Then
							If Not IsModInList(strMods,M(withMod)) Then
								modAdded = False
								Exit For
							End If
						Else
							If Not IsModInList(strMods, modPrefix & withMod) Then
								modAdded = False
								Exit For
							End If
						End If
					Next
					If modAdded Then AddModAndDepsToString(mo)
				End If
			Next
		Loop
	End Sub

	Sub myQuit
		window.close() : WScript.Quit
	End Sub

	Sub Launch
		' запускается при нажатии на кнопку запуска игры
		If isStarting Then
			msgbox "Already starting"
		Else
			isStarting = True
			Dim s, i, StartPriorities
			' параметры для команды start
			StartPriorities = Array("low","belownormal","normal","abovenormal","high")

'			i = "userconfig\.vars.ArmA2OAProfile"
'			If fso.FileExists(i) Then
'				s = ""
'				On Error Resume Next
'				s = WshShell.RegRead("HKCU\Software\Bohemia Interactive Studio\ArmA 2 OA\Player Name")
'				On Error Goto 0
'				If "" <> s Then
'					If     fso.FileExists(WshShell.SpecialFolders("MyDocuments") & "\ArmA 2\" & s & ".ArmA2OAProfile") Then
'						s = WshShell.SpecialFolders("MyDocuments") & "\ArmA 2\" & s & ".vars.ArmA2OAProfile"
'					ElseIf fso.FileExists(WshShell.SpecialFolders("MyDocuments") & "\ArmA 2 Other Profiles\" & s & "\" & s & ".ArmA2OAProfile") Then
'						s = WshShell.SpecialFolders("MyDocuments") & "\ArmA 2 Other Profiles\" & s & "\" & s & ".vars.ArmA2OAProfile"
'					End If
'				ElseIf fso.FolderExists(WshShell.SpecialFolders("MyDocuments") & "\ArmA 2") Then
'					For Each f in fso.GetFolder(WshShell.SpecialFolders("MyDocuments") & "\ArmA 2").Files
'						If InStr(2, f.Name, ".ArmA2OAProfile", 1) And 0 = InStr(1 ,f.Name, ".vars.ArmA2OAProfile", 1) Then
'							s = Replace(f.Name, ".ArmA2OAProfile", "")
'							s = WshShell.SpecialFolders("MyDocuments") & "\ArmA 2\" & s & ".vars.ArmA2OAProfile"
'							Exit For
'						End If
'					Next
'				End If
'				If InStr(s,"\") Then
'					If Not fso.FileExists(s) Then
'						fso.CopyFile i, s
'					ElseIf fso.GetFile(s).Size < fso.GetFile(i).Size Then
'						fso.CopyFile i, s
'					End If
'				Else
'					msgbox "Profile not found, ACE settings not saved"
'				End If
'			End If
'
'			If Not Checked.Exists("Beta") Then
'				strEXE = "arma2oa.exe"
'			Else
'				strEXE = "Expansion\beta\arma2oa.exe"
'			End If

			If 0 = GetHTMLValue("x64") Then
				strEXE = ""
			Else
				strEXE = "_x64"
			End If
			If fso.FileExists("arma3.exe") Then
				strEXE = "arma3" & strEXE & ".exe"
			ElseIf fso.FileExists("arma3server.exe") Then
				If Not headless.checked Then
					strEXE = "arma3server" & strEXE & ".exe ""-profiles=profiles"" -config=server.cfg -loadMissionToMemory"
					If fso.FolderExists("servermod\addons") Then
						strEXE = strEXE & " -servermod=servermod"
					End If
				Else
					strEXE = "arma3server" & strEXE & ".exe ""-profiles=hcprofiles"" -client -connect=127.0.0.1 -password=" & password.value
				End If
			Else
				msgbox "exe not found"
				myQuit
			End If
			strEXE = strEXE & " -nosplash -world=empty -skipIntro -noPause"
			strEXE = "cmd /c start """" /" & StartPriorities(2+CInt(GetHTMLValue("Priority"))) & " " & strEXE
			strMods = ""
			If fso.FolderExists("R:\@_RAM") Then strMods = ";" & "R:\@_RAM" & strMods
			' сначала пройдемся по всем отмеченным модам
			For Each i In M.Keys
				If Checked.Exists(i) Then AddModAndDepsToString(i)
				'i = "@ASR_AI3" Or 
				If ( i = "@pooter_enhanced_asr_ai" Or i = "@Vcom_AI" ) And Checked.Exists(i) Then strEXE = strEXE & " -filePatching"
			Next
			' потом по модам с опцией with:
			AddModsWith_with
			strEXE = strEXE & GetOptionsString
			' моды в кавычки на случай пробелов
			If ";" = Left(strMods,1) Then strMods = " ""-mod=" & Mid(strMods,2) & """"

			ModSum.ReadOnly = True
			idStartButton.Disabled = True
			If "DAD" = WshShell.ExpandEnvironmentStrings("%ComputerName%") Then MsgBox "Звук переключи"
			If "DEEP" = WshShell.ExpandEnvironmentStrings("%ComputerName%") Then
				If 1 = GetHTMLValue("x64") And fso.FileExists(cacheFilePath & ModSum.value & "\a.dat") Then
					If fso.GetFile(cacheFilePath & cacheFileName).DateLastModified <> fso.GetFile(cacheFilePath & ModSum.value & "\a.dat").DateLastModified Then
						fso.CopyFile cacheFilePath & ModSum.value & "\a.dat", cacheFilePath & cacheFileName, True
					End If
				End If
				window.clearTimeout(cacheTimer)
				cacheTimer = window.setTimeout( "CheckCache", 3*60*1000, "VBScript")
			End If
			If "" <> GetHTMLValue("startBefore") Then WshShell.Run GetHTMLValue("startBefore"), , True
			'msgbox strEXE & strMods
			'If subFolderMode Then msgbox strMods
			WshShell.Run strEXE & strMods, , True
			idTimer = window.setTimeout( "Launch_finished", 10000, "VBScript")
			'Launch_finished
			'myQuit
		End If
	End Sub

	Sub Launch_finished
		' запускается из функции Launch
		window.clearTimeout(idTimer)
		ModSum.ReadOnly = False
		idStartButton.Disabled = False
		isStarting = False
	End Sub

	Sub OnKeyDown
		' запускается при нажатии любой клавиши в окне
		If window.event.Keycode = 27 Then myQuit
		If window.event.Keycode = 13 And Not idStartButton.Disabled And "idStartButton" <> window.event.srcElement.id Then Launch
	End Sub

	Sub CheckCache
		' если изменился файл в AnimDataCache, сохраняет его
		window.clearTimeout(cacheTimer)
		If Not fso.FolderExists(cacheFilePath & ModSum.value) Then fso.CreateFolder cacheFilePath & ModSum.value
		If Not fso.FileExists(cacheFilePath & ModSum.value & "\a.dat") Then
			fso.CopyFile cacheFilePath & cacheFileName, cacheFilePath & ModSum.value & "\a.dat"
		ElseIf fso.GetFile(cacheFilePath & cacheFileName).DateLastModified <> fso.GetFile(cacheFilePath & ModSum.value & "\a.dat").DateLastModified Then
			fso.CopyFile cacheFilePath & cacheFileName, cacheFilePath & ModSum.value & "\a.dat", True
		End If
	End Sub


	Function GetHTMLValue(id)
		' возвращает значение данного элемента (в т.ч. checkbox)
		Dim ret
		If "checkbox" <> document.getElementById(id).type Then
			ret = document.getElementById(id).value
		Else
			If document.getElementById(id).checked Then
				ret = "1"
			Else
				ret = "0"
			End If
		End If
		GetHTMLValue = ret
	End Function

	Sub SetHTMLValue(id,val)
		' устанавливает для данного элемента данное значение (в т.ч. checkbox)
		If "checkbox" <> document.getElementById(id).type Then
			document.getElementById(id).value = val
		Else
			If "1" = val Then
				document.getElementById(id).checked = True
			Else
				document.getElementById(id).checked = False
			End If
		End If
	End Sub

	Function GetOptionsString
		' возвращает строку с параметрами, соответствующими текущим опциям запуска
		Dim ret, opt
		ret = ""
		For Each opt In Options.Keys
			If Options(opt)(2) Then
				If "checkbox" = document.getElementById(opt).type Then
					If "1" = GetHTMLValue(opt) Then ret = ret & " -" & opt
				Else
					If 0 <> GetHTMLValue(opt) Then ret = ret & " -" & opt & "=" & GetHTMLValue(opt)
				End If
			End If
		Next 
		GetOptionsString = ret
	End Function

	Sub SaveOnClick
		' при нажатии кнопки "Сохранить"
		Dim opt
		Set f = fso.CreateTextFile(optionsFile,True)
		f.WriteLine("[User]")
		For Each opt In Options.Keys
			f.WriteLine(opt & "=" & GetHTMLValue(opt))
		Next 
		f.Close
	End Sub

	Sub LoadOptions
		' загружает опции из файла
		Dim s, a
		If fso.FileExists(optionsFile) Then
			Set f = fso.OpenTextFile(optionsFile)
			Do Until f.AtEndOfStream
				s = f.ReadLine
				a = Split(s,"=")
				If 1 = UBound(a) Then
					If Options.Exists(a(0)) Then
						regEx.Pattern = Options(a(0))(0)
						If regEx.Test(a(1)) Then
							Call SetHTMLValue(a(0),a(1))
						Else
							Call SetHTMLValue(a(0),Options(a(0))(1))
						End If
					End If
				End If
			Loop
			f.Close
		End If
	End Sub

	Sub LoadOnClick
		' при нажатии кнопки "Загрузить"
		If Not fso.FileExists(optionsFile) Then
			msgbox "save not found"
		Else
			LoadOptions
		End If
	End Sub

	Sub HeadlessOnClick
		' при изменении галки Headless
		If headless.checked Then
			password_desc.style.visibility = "visible"
			password.style.visibility = "visible"
			password.focus
		Else
			password_desc.style.visibility = "hidden"
			password.style.visibility = "hidden"
		End If
	End Sub

	Sub UpdateOnClick
		' при нажатии кнопки "Update"
		Dim path
		path = document.location.href
		path = fso.GetParentFolderName(UnEscape(Replace(Right(path, Len(path) - 8), "/", "\")))
		path = path & "\cwRsync\"
		WshShell.CurrentDirectory = path
		WshShell.Run """" & "Download_from_inet.cmd" & """", , True
	End Sub
</SCRIPT>

<body bgcolor="buttonface" onKeyDown="OnKeyDown">
	<span id="ModsTable" style="overflow: auto; width: 770px; height: 400;"></span>
	<br><br>
	<div align="center">
		<input type="text" value="0" maxlength="8" size="15" id="ModSum" onKeyDown="ModSum_KeyDown">
		<input type="button" value="Start game" id="idStartButton" onClick="Launch">
		<label for="headless" id="headless_label"><input id="headless" type="checkbox" onClick="HeadlessOnClick">Headless Client</label>
		<span id="password_desc" title="Server password">pass:</span><input type="text" value="777" maxlength="200" size="10" id="password" title="Server password">
		<input type="button" value="Update" id="idUpdate" onClick="UpdateOnClick">
	</div>
	<fieldset>
	<legend>Personal settings</legend>
	<div align="center">
	Start before
	<input type="text" value="" maxlength="2000" size="100" id="startBefore">
	<input type="button" value="  X  " onClick="startBefore.value=''">
	<br />
	<label for="x64"><input id="x64" type="checkbox">x64</label>
	cpuCount
	<select id="cpuCount">
		<option value=0>not set
		<option value=1>1
		<option value=2>2
		<option value=3>3
		<option value=4>4
		<option value=5>5
		<option value=6>6
		<option value=7>7
		<option value=8>8
		<option value=9>9
		<option value=10>10
		<option value=11>11
		<option value=12>12
		<option value=13>13
		<option value=14>14
		<option value=15>15
		<option value=16>16
	</select>
	<!--maxMem
	<select id="maxMem">
		<option value=0>not set
		<option value=768>768
		<option value=1024>1024
		<option value=2047>2047
		<option value=4096>4096
		<option value=8192>8192
		<option value=16384>16384
		<option value=32768>32768
	</select>
	-->Priority
	<select id="Priority">
		<option value="-2">low
		<option value="-1">below normal
		<option value="0">normal
		<option value="1" selected="">above normal
		<option value="2">high
	</select>
	<label for="showScriptErrors"><input id="showScriptErrors" type="checkbox">Show script errors</label>
	<label for="nologs"><input id="nologs" type="checkbox">No logs</label>
	<br /><br />
	<input type="button" value="Save" onClick="SaveOnClick">
	<input type="button" value="Load" onClick="LoadOnClick">
	</div>
	</fieldset>
</body>
</html>
